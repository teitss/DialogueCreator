buildscript {
    ext.kotlin_version = '1.2.71'
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

group 'io.github.teitss'
version '1.0.0'
description 'A plugin to create Pixelmon Reforged dialogues.'
archivesBaseName = 'DialogueCreator'

sourceCompatibility = targetCompatibility = "1.8"
compileJava.options.encoding = "UTF-8"

reobf {
    shadowJar {}
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'kotlin', 'repack.kotlin'
}
configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}
minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
    kapt 'org.spongepowered:spongeapi:7.1.0'
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}